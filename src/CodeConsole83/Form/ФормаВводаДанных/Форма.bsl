
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОписаниеДанных = Параметры.ОписаниеДанных;
	
	МассивРеквизитов = Новый Массив;
	
	Для Каждого Колонка из ОписаниеДанных Цикл

		РеквизитФормы = Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов(Колонка.Тип));
		//РеквизитФормы = Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов(Колонка.ТипЗначения, , "NULL"), ИмяТаблицы);
		МассивРеквизитов.Добавить(РеквизитФормы);
		
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(МассивРеквизитов);
	
	Обработка = ПолучитьОбработку();
	
	Для Каждого Реквизит Из ОписаниеДанных Цикл 
		
		ОписаниеЭлемента = Новый Структура;
		
		ОписаниеЭлемента.Вставить("Вид", ВидПоляФормы.ПолеВвода);
		
		Обработка.ДополнитьСтруктуру(ОписаниеЭлемента, Реквизит);
		
		Если Реквизит.Свойство("Вид") И ТипЗнч(Реквизит.Вид) = Тип("Строка") Тогда 
			ОписаниеЭлемента.Вставить("Вид", Вычислить("ВидПоляФормы." + Реквизит.Вид));
		КонецЕсли;
		ОписаниеЭлемента.Вставить("Имя", Реквизит.Имя);
		ОписаниеЭлемента.Вставить("Тип", Тип("ПолеФормы"));
		ОписаниеЭлемента.Вставить("ПутьКДанным", Реквизит.Имя);
		
		Обработка.СоздатьЭлементыУправления(ЭтаФорма, , ОписаниеЭлемента, Элементы.ГруппаРеквизиты);
		
	КонецЦикла;
	
	Если Параметры.Свойство("Заголовок") Тогда 
		Заголовок = Параметры.Заголовок;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОбработку(Знач СостояниеАдрес = Неопределено)

	Обработка = ВнешниеОбработки.Создать("CodeConsole83");
	
	Если СостояниеАдрес <> Неопределено Тогда 
		Обработка.ВосстановитьСостояние(СостояниеАдрес);
	КонецЕсли;
	
	Возврат Обработка;
	
КонецФункции // ПолучитьОбработку()

&НаКлиенте
Процедура Сохранить(Команда)
	
	Успешно = Истина;
	
	Результат = Новый Структура;
	Для Каждого Реквизит Из Параметры.ОписаниеДанных Цикл 
		Если НЕ ЗначениеЗаполнено(ЭтаФорма[Реквизит.Имя]) И Реквизит.Обязательное Тогда 
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Не заполнено обязательное поле ""%1""", Реквизит.Имя);
			Сообщение.Поле = Реквизит.Имя;
			Сообщение.УстановитьДанные(ЭтаФорма);
			Сообщение.Сообщить();
			Успешно = Ложь;
			
		КонецЕсли;
		
		Результат.Вставить(Реквизит.Имя, ЭтаФорма[Реквизит.Имя]);
		
	КонецЦикла;
	
	Если НЕ Успешно Тогда 
		Возврат;
	КонецЕсли;
	Закрыть(Результат);
	
КонецПроцедуры
