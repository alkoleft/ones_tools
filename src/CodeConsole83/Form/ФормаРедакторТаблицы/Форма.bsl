&НаКлиенте
Перем БиблиотекаМетодов;

&НаКлиенте
Процедура Инициализировать(пБиблиотекаМетодов) Экспорт 
	
	БиблиотекаМетодов = пБиблиотекаМетодов;
	
КонецПроцедуры

#Область События_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресДанныхТЗ = Параметры.АдресДанныхТЗ;
	
	Таблица = ПолучитьИзВременногоХранилища(АдресДанныхТЗ);
	
	Если Таблица <> Неопределено Тогда 
		
		ВнешниеОбработки.Создать("CodeConsole83").СоздатьКолонкиТЗ(ЭтаФорма, "ТаблицаДанных", Таблица.Колонки);
		
		ТаблицаДанных.Загрузить(Таблица);
	
	Иначе
		
		ОткрытьФормуНастроек = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОткрытьФормуНастроек Тогда 
		
		НастроитьКолонки(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти //События_формы

&НаСервере
Процедура ОбновитьТЗ(ОписаниеКолонок)
	
	ТаблицаПосле = Новый ТаблицаЗначений;
	
	ТаблицаДо = РеквизитФормыВЗначение("ТаблицаДанных");
	
	Для Каждого Колонка Из ОписаниеКолонок Цикл 
		
		ТаблицаПосле.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения, Колонка.Заголовок);
		
	КонецЦикла;
	
	Если ТаблицаДо <> Неопределено Тогда
		
		Для инд = 0 По ТаблицаДо.Количество() - 1 Цикл 
			
			Стр = ТаблицаПосле.Добавить();
			
			ЗаполнитьЗначенияСвойств(Стр, ТаблицаДо[инд]);
			
		КонецЦикла;

	КонецЕсли;	
	
	ВнешниеОбработки.Создать("CodeConsole83").ОбновитьКолонкиТЗ(ЭтаФорма, "ТаблицаДанных", ?(ТаблицаДо = Неопределено, Неопределено, ТаблицаДо.Колонки), ТаблицаПосле.Колонки);
	
	ТаблицаДанных.Загрузить(ТаблицаПосле);
	
КонецПроцедуры

#Область Команды

&НаКлиенте
Процедура НастроитьКолонки(Команда)

	Оповещение = Новый ОписаниеОповещения("ПослеНастройкиКолонок", ЭтотОбъект);
	ОткрытьФорму("ВнешняяОбработка.CodeConsole83.Форма.ФормаНастройкаТаблицы", Новый Структура("Данные", РеквизитыТаблицы), ЭтаФорма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеНастройкиКолонок(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат <> Неопределено Тогда 
		
		ОбновитьТЗ(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьТаблицу(Команда)
	
	СохранитьДанныеТаблицыНаСервере();
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеТаблицыНаСервере()
	
	ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("ТаблицаДанных"), АдресДанныхТЗ);
	
КонецПроцедуры
#КонецОбласти //Команды

